
group = "com.github.parboiled1";
version = "1.0.0-beta.5-SNAPSHOT";

project.ext {
    description = "grappa: support tools (byte code, performance tests)";
    sourceCompatibility = "1.7"; // we have to, for procyon
    targetCompatibility = "1.7"; // defaults to sourceCompatibility
};

buildscript {
    repositories {
        maven { url "http://repo.spring.io/plugins-release" };
    }
    dependencies {
        classpath(group: "org.springframework.build.gradle", 
            name: "propdeps-plugin", version: "0.0.6");
    }
}

apply(plugin: "java");
apply(plugin: "maven");
apply(plugin: "signing");
apply(plugin: "osgi");
apply(plugin: "idea");
apply(plugin: "eclipse");
apply(plugin: "propdeps");
apply(plugin: "propdeps-maven");
apply(plugin: "propdeps-idea");
apply(plugin: "propdeps-eclipse");


repositories {
    mavenCentral();
    mavenLocal();
    jcenter();
}

dependencies {
    provided(group: "com.google.code.findbugs", name: "jsr305",
        version: "2.0.1");
    compile(group: "com.github.parboiled1", name: "grappa",
        version: "1.0.0-beta.6");
    compile(group: "org.bitbucket.mstrobel", name: "procyon-core",
        version: "0.5.25");
    compile(group: "org.bitbucket.mstrobel", name: "procyon-compilertools",
        version: "0.5.25");
    compile(group: "com.google.caliper", name: "caliper",
        version: "1.0-beta-SNAPSHOT") {
        exclude(group: "com.google.guava", module: "guava");
        exclude(group: "org.ow2.asm", module: "asm");
        exclude(group: "org.ow2.asm", module: "asm-analysis");
        exclude(group: "org.ow2.asm", module: "asm-commons");
        exclude(group: "org.ow2.asm", module: "asm-tree");
        exclude(group: "org.ow2.asm", module: "asm-util");
        exclude(group: "org.ow2.asm", module: "asm-xml");
        exclude(group: "asm", module: "asm");
    };
    compile(group: "com.sun.jersey", name: "jersey-core", version: "1.18.1");
}

javadoc.options.links("http://docs.oracle.com/javase/6/docs/api/");
javadoc.options.links("http://jsr-305.googlecode.com/svn/trunk/javadoc/");
javadoc.options.links("http://asm.ow2.org/asm40/javadoc/user/");
javadoc.options.links("http://docs.guava-libraries.googlecode.com/git-history/v17.0/javadoc/");

/*
 * Necessary to generate the source and javadoc jars
 */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources";
    from sourceSets.main.allSource;
}

/*
 * Javadoc: we need to tell where the overview.html is, it will not pick it up
 * automatically...
 */

//javadoc {
//    options.overview = "src/main/java/overview.html";
//}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc";
    from javadoc.destinationDir;
}

artifacts {
    archives jar;
    archives sourcesJar;
    archives javadocJar;
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.11";
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip";
}

task pom << {
    pom {}.writeTo("${projectDir}/pom.xml");
}

/*
 * SIGNING
 */

project.ext {
    gitrwscm = sprintf("git@github.com:parboiled1/%s", name);
    gitroscm = sprintf("https://github.com/parboiled1/%s.git", name);
    projectURL = sprintf("https://github.com/parboiled1/%s", name);
    sonatypeStaging = "https://oss.sonatype.org/service/local/staging/deploy/maven2/";
    sonatypeSnapshots = "https://oss.sonatype.org/content/repositories/snapshots/";
};

task checkSigningRequirements << {
    def requiredProperties = [ "sonatypeUsername", "sonatypePassword" ];
    def noDice = false;
    requiredProperties.each {
        if (project.properties[it] == null) {
            noDice = true;
            System.err.printf("property \"%s\" is not defined!", it);
        }
    }
    if (noDice)
        throw new IllegalStateException("missing required properties for " +
            "upload");
}

uploadArchives {
    dependsOn(checkSigningRequirements);
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment);
            }

            repository(url: "${sonatypeStaging}") {
                authentication(
                    userName: project.properties["sonatypeUsername"],
                    password: project.properties["sonatypePassword"]
                );
            }

            snapshotRepository(url: "${sonatypeSnapshots}") {
                authentication(
                    userName: project.properties["sonatypeUsername"],
                    password: project.properties["sonatypePassword"]
                );
            }
        }
    }
}

/*
 * Configure pom.xml on install, uploadArchives
 */
[
    install.repositories.mavenInstaller,
    uploadArchives.repositories.mavenDeployer
]*.pom*.whenConfigured { pom ->
    pom.project {
        name "${name}";
        packaging "jar";
        description "${description}";
        url "${projectURL}";

        scm {
            url "${gitrwscm}";
            connection "${gitrwscm}";
            developerConnection "${gitroscm}";
        }

        licenses {
            license {
                name "Apache Software License, version 2.0";
                url "http://www.apache.org/licenses/LICENSE-2.0";
                distribution "repo";
            }
        }

        developers {
            developer {
                id "fge";
                name "Francis Galiegue";
                email "fgaliegue@gmail.com";
            }
        }
    }
}

ext.forRelease = !version.endsWith("-SNAPSHOT");
signing {
    required { forRelease && gradle.taskGraph.hasTask("uploadArchives") };
    sign configurations.archives;
}

